/**
 * Load Orgzly-specific properties from app.properties.
 * File is not included, see sample.app.properties file.
 */
def loadAppProperties() {
    Properties properties = new Properties()

    def file = new File(settingsDir, 'app.properties')

    if (file.exists()) {
        properties.load(file.newDataInputStream())
    } else {
        logger.warn("Properties file ${file} does not exist")
    }

    return properties
}

gradle.ext.appProperties = loadAppProperties()

// Setup org-java project if "org_java" is set to an existing directory.
if (gradle.ext.appProperties.org_java_directory?.trim()) {
    def path = gradle.ext.appProperties.org_java_directory

    def dir = path.startsWith('/') ? new File(path) : new File(settingsDir, path)

    if (dir.exists()) {
        include ':org-java'
        project(':org-java').projectDir = dir
        gradle.ext.orgJavaDir = dir
    } else {
        logger.warn("Ignoring non-existent directory $dir")
    }
}

include ':app'
